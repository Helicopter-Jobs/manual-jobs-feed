=TEXTJOIN(CHAR(10),TRUE,
"<html>",
"<head>",
"<meta charset='UTF-8'>",
"<title>"&Config!B1&"</title>",
"</head>",
"<body>",
"<h1>"&Config!B1&"</h1>",
"<p>"&Config!B2&"</p>",
"",
TEXTJOIN(CHAR(10),TRUE,
FILTER(
ARRAYFORMULA(
"<article class='job'>"&CHAR(10)&
"<h2>"&'Form Responses 1'!B2:B&"</h2>"&CHAR(10)&
"<p class='company'><strong>Company:</strong> "&'Form Responses 1'!C2:C&"</p>"&CHAR(10)&
"<p class='location'><strong>Location:</strong> "&'Form Responses 1'!D2:D&"</p>"&CHAR(10)&
"<p><a class='apply' href='"&'Form Responses 1'!E2:E&"'>Apply Here</a></p>"&CHAR(10)&
"<div class='description'>"&CHAR(10)&
IF('Form Responses 1'!G2:G<>"","<p><strong>Schedule:</strong> "&'Form Responses 1'!G2:G&"</p>","")&
IF('Form Responses 1'!H2:H<>"","<p><strong>Compensation:</strong> "&'Form Responses 1'!H2:H&"</p>","")&
IF('Form Responses 1'!I2:I<>"","<p><strong>Aircraft:</strong> "&'Form Responses 1'!I2:I&"</p>","")&
"<p class='category-hint'>"&'Form Responses 1'!F2:F&"</p>"&CHAR(10)&
"<p>"&SUBSTITUTE('Form Responses 1'!J2:J,CHAR(10),"</p><p>")&"</p>"&CHAR(10)&
"</div>"&CHAR(10)&
"</article>"
),
'Form Responses 1'!B2:B<>""
)
),
"",
"<hr>",
"<p>"&Config!B3&"</p>",
"</body>",
"</html>"
)

